#!ipxe

###
### netboot.xyz-custom menu example
###

:custom
clear custom_choice
menu This is a Test Menu
item --gap This is the first sub menu
item option_one ${space} Loading a kernel and initrd
item option_two ${space} Loading an ISO
item --gap This is a second sub menu
item option_three ${space} Loads another custom sub menu
item live_menu ${space} This is ProxMox install
choose custom_choice || goto custom_exit
echo ${cls}
goto ${custom_choice}
goto custom_exit

:option_one
kernel http://path.to/vmlinuz
initrd http://path.to/initrd
imgargs vmlinuz put_kernel_img_args_here
boot || goto custom_exit

:option_two
kernel https://boot.netboot.xyz/memdisk raw iso
initrd http://192.168.1.94:50000/Windows-Tweaks.info_Windows_7_32-bit_Recovery-CD.iso
boot || goto custom_exit

:option_three
echo Chains into another menu...
chain http://192.168.1.94:50000/boot.ipxe || goto custom

:custom_exit
chain utils.ipxe
exit

:live_menu
# Use Local Override
set live_endpoint http://192.168.1.237:8000

set os Proxmox
set pve_version 8.3-1
set pve_opts vga=791 video=vesafb:ywrap,mtrr ramdisk_size=16777216 rw quiet initrd=initrd
set pve_url ${live_endpoint}/proxmox/${pve_version}

menu ${os}
item --gap ${os} - ${pve_version}
item normal ${space} Install Proxmox ${pve_version}
item debug ${space} Install Proxmox ${pve_version} (Debug Mode)
choose live_version || goto live_exit
imgfree
goto ${live_version}

:debug
kernel ${pve_url}/linux26 ${pve_opts} splash=verbose proxdebug ${cmdline}
goto boot

:normal
kernel ${pve_url}/linux26 ${pve_opts} splash=silent ${cmdline}
goto boot

:boot
initrd ${pve_url}/initrd
boot

:live_exit
clear menu
exit 0
